plugins {
  id 'application'
}

group 'pl.pwr'
version '1.0-SNAPSHOT'

repositories {
  mavenCentral()
}

configurations {
  frontendApp
}

task copyFrontendApp(type: Copy) {
  from configurations.frontendApp
  into "$buildDir/frontendApp/frontend"
}

build.dependsOn(copyFrontendApp)
run.dependsOn(build)

sourceSets {
  main {
    compileClasspath += files("$buildDir/frontendApp")
    runtimeClasspath += files("$buildDir/frontendApp")
  }
}

dependencies {
  implementation 'io.javalin:javalin-bundle:4.4.0'
  implementation 'com.google.code.gson:gson:2.9.0'
  implementation 'com.typesafe:config:1.4.2'
  frontendApp project(path: ":frontend", configuration: 'webResources')
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

application {
  mainClass = 'pwr.edu.app.Application'
}

task srcZip(type: Zip) {
  outputs.upToDateWhen {false} // Always run

  from projectDir
  archiveFileName = "lab-7.zip"
  include 'src/**/*', 'build.gradle'
  destinationDirectory = layout.buildDirectory.dir('eportal-upload')

  doLast {
    println "Path to zip: $archivePath.path"
  }
}

test {
  useJUnitPlatform()
}
